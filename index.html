<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8" />
		<title>Template</title>
<script id="vshader" type="x-shader/x-vertex">
attribute vec2 a_position;
varying vec2 v_position;

void main()
{
	v_position = a_position;
	gl_Position = vec4(a_position.xy, 0.0, 1.0);
}
</script>
	
<script id="fshader" type="x-shader/x-fragment">
// ****************** The base code was taken from https://github.com/lightbits/ray-march ******************
// ****************** The scene() function is from me, Jonathan ******************

// A quad is uploaded extending from -1 to 1 on both axes
// The v_position variable interpolates between the quad vertices
precision mediump float;

// Camera
uniform float u_aspectRatio;
uniform vec3 u_position;
uniform vec3 u_rotation;

// Scene
uniform vec3 u_lightPosition;
uniform vec4 u_lightColor;
uniform int u_shaderScene; // This sets the sdfs to be displayed
uniform float u_roundBox;
uniform bool u_checkerboard;

varying vec2 v_position;

// Raymarch parameters
const vec3 right = vec3(1.0, 0.0, 0.0);
const vec3 upward = vec3(0.0, 1.0, 0.0);
const vec3 forward = vec3(0.0, 0.0, 1.0);
const float u_far = 15.0;
const float u_focalLength = 1.67;
const vec4 u_skyColor = vec4(0.2, 0.4, 0.6, 1.0);
const vec4 u_ambient = vec4(0.1, 0.2, 0.3, 1.0);
const float u_rmEpsilon = 0.001; // Distance threshold
const int u_rmSteps = 100; // Max steps
int u_shapeColor = 0; // index number for the color of the shape if any

// ********************************* Start Constants *********************************

#define COLOR_OTHER 0
#define COLOR_PLANE 1
#define COLOR_SPHERE_ONE 2
#define COLOR_SPHERE_TWO 3

// Operation Union
float opUnion(float d1, float d2)
{
	return min(d1, d2);
}

// Operation Union Color
vec2 opUnionColor(vec2 d1, vec2 d2)
{
	if (d1.x < d2.x)
	{
		return d1;
	}
	else
	{
		return d2;
	}
}

// Operation Subtraction
float opSubtract(float a, float b)
{
	return max(-b, a);
}

// MARK: ** Quicker and better optimized Transformations than a matrix **

float length2Nth(vec2 p, float n)
{
	return pow(pow(p.x, n) + pow(p.y, n), 1.0 / n);
}

// Rotates a point t radians around the x-axis
vec3 rotateX(vec3 v, float t)
{
	float cost = cos(t); float sint = sin(t);
	return vec3(v.x, v.y * cost - v.z * sint, v.y * sint + v.z * cost);
}

// Rotates a point t radians around the y-axis
vec3 rotateY(vec3 v, float t)
{
	float cost = cos(t); float sint = sin(t);
	return vec3(v.x * cost + v.z * sint, v.y, - v.x * sint + v.z * cost);
}

// Rotates a point t radians around the z-axis
vec3 rotateZ(vec3 v, float t)
{
	float cost = cos(t); float sint = sin(t);
	return vec3(v.x * cost - v.y * sint, v.x * sint + v.y * cost, v.z);
}

// MARK: ** Signed Shapes **

// Signed
// Sphere
float sdfSphere(vec3 p, float r)
{
	return length(p) - r;
}

// n.xyz = point on plane
// n.w   = distance to plane
// Note: N must be normalized!
float sdfPlane( vec3 p, vec4 n )
{
	return dot( p, n.xyz ) + n.w;
}

float sdfBox(vec3 p, vec3 b)
{
	vec3 q = abs(p) - b;
	return length(max(q, 0.0)) + min(max(q.x, max(q.y, q.z)), 0.0);
}

float sdfRoundBox(vec3 p, vec3 b, float r)
{
	vec3 q = abs(p) - b;
	return length(max(q, 0.0)) + min(max(q.x,max(q.y, q.z)), 0.0) - r;
}

// t.x = outer t.y = inner
float sdfTorus(vec3 p, vec2 t)
{
	vec2 q = vec2(length(p.xz) - t.x, p.y);
	return length(q) - t.y;
}

float sdfTorus82( vec3 p, vec2 t)
{
	vec2 q = vec2(length2Nth(p.xz, 2.0) - t.x, p.y);
	return length2Nth(q, 8.0) - t.y;
}

float sdfTorus88(vec3 p, vec2 t)
{
	vec2 q = vec2(length2Nth(p.xz, 8.0) - t.x, p.y);
	return length2Nth(q, 8.0) - t.y;
}

// MARK: ** Everything Else **

// Defines the distance field for the scene
float scene(vec3 p)
{
	p = rotateX(p, u_rotation.x);
	p = rotateY(p, u_rotation.y);
	p = rotateZ(p, u_rotation.z);
	
	// This and a small number for ambient occlusion fixes the black normal vectors
	p *= 0.8;
	
	// w = is the distance the .xyz is the direction
	vec2 plane = vec2(sdfPlane(p, normalize(vec4(0.0, 1.0, 0.0, 2.0))), COLOR_PLANE);
	float shapes;
	
	if (u_shaderScene == 0)
	{
		// Just a sphere
		float sphere1 = sdfSphere(p + vec3(-2.0, 0.0, 0.0), 1.0);
		float sphere2 = sdfSphere(p + vec3( 2.0, 0.0, 0.0), 1.0);
		
		vec2 spheres = opUnionColor(vec2(sphere1, COLOR_SPHERE_ONE), vec2(sphere2, COLOR_SPHERE_TWO));
		vec2 colorShapes = opUnionColor(spheres, plane);
		u_shapeColor = int(colorShapes.y);
		
		return colorShapes.x;
	}
	// RoundedBox changes from sphere to box and back
	else if (u_shaderScene == 1)
	{
		shapes = sdfRoundBox(p, vec3(u_roundBox), -(u_roundBox - 1.0));
	}
	// Repeated Boxes On The Ground
	else if (u_shaderScene == 2)
	{
		vec3 pos = p;
		pos.xz = mod(p.xz, 1.0) - vec2(0.5);
		shapes = sdfBox(pos + vec3(0.0, 1.5, 0.0), vec3(0.25));
	}
	// Repeated Spheres In All Directions
	else if (u_shaderScene == 3)
	{
		vec3 pos = mod(p.xyz, 1.0) - vec3(0.5);
		shapes = sdfSphere(pos, 0.25);
	}
			// A Sphere with boxes subtracted from it
	else if (u_shaderScene == 4)
	{
		float db = sdfSphere(p , 2.0);
		vec3 pos = mod(p.xyz, 0.5) - vec3(0.25);
		shapes = opSubtract(db, sdfBox(pos , vec3(0.2)));
	}
	// One Box with a twist ;-)
	else if (u_shaderScene == 5)
	{
		vec3 pos = rotateY(p, 0.5 * p.y);
		shapes = sdfBox(pos, vec3(1.0, 2.0, 1.0));
	}
	// Twisted box with a sphere subtracted from it
	else if (u_shaderScene == 6)
	{
		float d2 = sdfSphere(p, 1.3);
		vec3 pos = rotateY(p, 0.5 * p.y);
		float d1 = sdfBox(pos, vec3(1.0, 2.0, 1.0));
		shapes = opSubtract(d1, d2);
	}
	// Torus
	else if (u_shaderScene == 7)
	{
		vec3 pos = rotateX(p, radians(90.0));
		shapes = sdfTorus( pos, vec2( 1.0, 0.5));
	}
	// Flat Torus
	else if (u_shaderScene == 8)
	{
		vec3 pos = rotateX(p, radians(90.0));
		shapes = sdfTorus82( pos, vec2( 1.0, 0.5));
	}
	// Square Flat Torus
	else if (u_shaderScene == 9)
	{
		vec3 pos = rotateX(p, radians(90.0));
		shapes = sdfTorus88(pos, vec2( 1.0, 0.5));
	}
	
	return opUnion(shapes, plane.x);
}

// ****** The New Ray Marcher ******
bool raymarch(vec3 origin, vec3 direction, out float total)
{
	total = 0.0;
	vec3 hitpoint = origin;
	
	for (int i = 0; i < u_rmSteps; i++)
	{
		// distance.x is the distance and distance.y is the color index
		float distance = scene(hitpoint);
		
		total += distance;
		hitpoint += distance * direction;
		
		if (distance <= u_rmEpsilon * total * 2.0)
		{
			return true;
		}
		
		if (total > u_far)
		{
			return false;
		}
	}
	
	return false;
}

// Approximates the (normalized) gradient of the distance function at the given point.
// If p is near a surface, the function will approximate the surface normal.
vec3 getNormal(vec3 p)
{
    vec2 e = vec2(0.001, 0.0);
	
	return normalize(vec3(
		scene(p + e.xyy) - scene(p - e.xyy),
		scene(p + e.yxy) - scene(p - e.yxy),
		scene(p + e.yyx) - scene(p - e.yyx)));
}

// Returns a value between 0 and 1 depending on how visible p0 is from p1
// 0 means it's completely blocked, 1 means completely visible
// k defines the hardness of the shadow
float getShadow(vec3 p0, vec3 p1, float k)
{
	vec3 rd = normalize(p1 - p0);
	float t = 10.0 * u_rmEpsilon; // Start a bit away from the surface
	float maxt = length(p1 - p0);
	float f = 1.0;
	
	for(int i = 0; i < u_rmSteps; ++i)
	{
		float d = scene(p0 + rd * t);

		// A surface was hit before we reached p1
		if(d < u_rmEpsilon)
			return 0.0;

		// Penumbra factor is calculated based on how close we were to
		// the surface, and how far away we are from the shading point
		// See http://www.iquilezles.org/www/articles/rmshadows/rmshadows.htm
		f = min(f, k * d / t);

		t += d;

		// We reached p1
		if(t >= maxt)
			break;
	}

	return f;
}

// Calculate the light intensity with soft shadows
// p: point on surface
// lightPos: position of the light source
// lightColor: the radiance of the light source
// returns: the color of the point
vec4 getShading(vec3 p, vec3 normal, vec3 lightPos, vec4 lightColor)
{
	float lightIntensity = 0.0;
	float shadow = getShadow(p, lightPos, 16.0);
	
	if(shadow > 0.0) // If we are at all visible
	{
		vec3 lightDirection = normalize(lightPos - p);
		lightIntensity = shadow * clamp(dot(normal, lightDirection), 0.0, 1.0);
	}
	
	return lightColor * lightIntensity + u_ambient * (1.0 - lightIntensity);
}

// Compute an ambient occlusion factor
// p: point on surface
// n: normal of the surface at p
// returns: a value clamped to [0, 1], where 0 means there were no other surfaces around the point,
// and 1 means that the point is occluded by other surfaces.
float ambientOcclusion(vec3 p, vec3 n)
{
	float stepSize = 0.01; // 0.01 (original) // looks better with 0.001
	float t = stepSize;
	float oc = 0.0;
	for(int i = 0; i < 15; ++i)
	{
		float d = scene(p + n * t);
		oc += t - d; // Actual distance to surface - distance field value
		t += stepSize;
	}

	return clamp(oc, 0.0, 1.0);
}

// Create a checkboard texture
vec4 getFloorTexture(vec3 p)
{
	p = rotateX(p, u_rotation.x);
	p = rotateY(p, u_rotation.y);
	p = rotateZ(p, u_rotation.z);

	vec2 m = mod(p.xz, 2.0) - vec2(1.0);
	return m.x * m.y > 0.0 ? vec4(0.1) : vec4(1.0);
}

vec4 computeColor(vec3 ro, vec3 rd)
{
	float distance; // Total distance
	
	//int i; // How many steps
	bool hit = raymarch(ro, rd, distance); // Raymarching
	
	if (hit)
	{
		vec3 normal; // Surface normal
		vec4 color; // Surface texture color

		// The "Shape Color" must come before any other colors
		if (u_shapeColor == COLOR_SPHERE_ONE)
		{
			color = vec4(2.0, 1.5, 0.0, 1.0);
		}
		else if (u_shapeColor == COLOR_SPHERE_TWO)
		{
			color = vec4(1.0, 0.0, 1.0, 1.0);
		}
		else
		{
			color = vec4(1.0);
		}
		
		vec3 p = rd * distance + ro; // Surface point
		
		// Add a checkerboard texture to the plane
		if (u_checkerboard)
		{
			color *= getFloorTexture(p);
		}
		
		normal = getNormal(p);
		
		// Lighting
		color *= (getShading(p, normal, u_lightPosition, u_lightColor) + getShading(p, normal, vec3(4.0, 4.0, 0.0), vec4(1.0, 0.5, 0.5, 1.0))) / 2.0;
		// ***** OpenGL Gold Book Linear Fog *****
		//algorithm: fog = (fou_end - p_eye.z) / (fou_end - fou_start);
		float fog = (u_far - distance) / (u_far - 1.0); // u_far equals 20.0
		fog = clamp(fog, 0.0, 1.0);
		
		color = mix(u_skyColor, color, fog);
		// ***** OpenGL Gold Book Linear Fog *****
		
		return color;
	}
	else
	{
		return u_skyColor;
	}
}

void main()
{
	// The position of the camera aka ray origin
	vec3 ro = u_position;
	// The angle of the camera aka ray direction
	vec3 rd = normalize(forward * u_focalLength + right * v_position.x * u_aspectRatio + upward * v_position.y);
	vec4 color = computeColor(ro, rd);
	
	gl_FragColor = vec4(color.xyz, 1.0);
}

</script>
</head>
	
<!-- Version 1.0 -->

<body onload="create()">
	<script src="webgl.js"></script>
	<script src="main.js"></script>
</body>
</html>

<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8" />
		<title>Raymarching Demo</title>
<script id="vshader" type="shader/vertex">#version 300 es
in vec4 a_vertices;

void main(void)
{
	gl_Position = a_vertices;
}

</script>

<script id="fshader" type="shader/fragment">#version 300 es
// ************************** Do Not Remove **************************
precision highp float;
uniform vec2 iResolution;
uniform vec2 iMouse;
uniform float iTime;
uniform float iTimeDelta;

// Keyboard Controls
uniform int u_floorColor; // The up arrow key
uniform int u_shaderScene; // The left arrow and right arrow keys

// The old gl_FragColor
out vec4 mainFragColor;
// ************************** Do Not Remove **************************

const int MAX_MARCHING_STEPS = 128;
const float MIN_DISTANCE = 0.0;
const float MAX_DISTANCE = 20.0;
const float MIN_FOG = 0.0;
const float MAX_FOG = 20.0;
const float PRECISION = 0.005;
const float EPSILON = 0.0001;

struct Material
{
	vec3 ambientColor; // float(k_a) * vec3(i_a)
	vec3 diffuseColor; // float(k_d) * vec3(i_d)
	vec3 specularColor; // float(k_s) * vec3(i_s)
	float specularIntensity;
	float shininess;
};

struct Surface
{
	int id;
	float distance;
	vec3 color;
};

Material blueLight(vec3 ambientColor)
{	
	vec3 diffuseColor = vec3(0.6, 0.8, 1.9);
	vec3 specularColor = vec3(1, 1, 1);
	float specularIntensity = 3.0;
	float shininess = 50.0;
	
	return Material(ambientColor, diffuseColor, specularColor, specularIntensity, shininess);
}

Material redLight(vec3 ambientColor)
{
	vec3 diffuseColor = vec3(2.0, 0.5, 0.5);
	vec3 specularColor = vec3(1, 1, 1);
	float specularIntensity = 1.0;
	float shininess = 5.0;
	
	return Material(ambientColor, diffuseColor, specularColor, specularIntensity, shininess);
}

// MARK: Operators

Surface opUnion(Surface shape1, Surface shape2)
{
	// The lesser distance should be the output
	if (shape1.distance > shape2.distance)
	{
		return shape2;
	}
	
	return shape1;
}

Surface opSubtract(Surface shape1, Surface shape2)
{
	if (-shape2.distance < shape1.distance)
	{
		return shape1;
	}
	
	shape2.distance = -shape2.distance;
	
	return shape2;
}

float length2Nth(vec2 p, float n)
{
	return pow(pow(p.x, n) + pow(p.y, n), 1.0 / n);
}

// MARK: Motion
// Rotates a point theta radians around the x-axis
vec3 rotateX(vec3 p, float theta)
{
	float cost = cos(theta); 
	float sint = sin(theta);
	
	return vec3(p.x, p.y * cost - p.z * sint, p.y * sint + p.z * cost);
}

// Rotates a point theta radians around the y-axis
vec3 rotateY(vec3 p, float theta)
{
	float cost = cos(theta); 
	float sint = sin(theta);
	
	return vec3(p.x * cost + p.z * sint, p.y, - p.x * sint + p.z * cost);
}

// Rotates a point theta radians around the z-axis
vec3 rotateZ(vec3 p, float theta)
{
	float cost = cos(theta); 
	float sint = sin(theta);
	
	return vec3(p.x * cost - p.y * sint, p.x * sint + p.y * cost, p.z);
}

// MARK: Shapes
float sdPlane(vec3 p, vec3 normal, float distance)
{
	// normal.xyz = point on plane must be normalized!
	// distance = distance to plane
	return dot(p, normalize(normal)) + distance;
}

float sdSphere(vec3 p, float r)
{
	return length(p) - r;
}

float sdRoundBox(vec3 p, vec3 b, float r)
{
	vec3 q = abs(p) - b;
	return length(max(q, 0.0)) + min(max(q.x,max(q.y, q.z)), 0.0) - r;
}

float sdBox(vec3 p, vec3 b)
{
	vec3 q = abs(p) - b;
	return length(max(q, 0.0)) + min(max(q.x, max(q.y, q.z)), 0.0);
}

// t.x = outer t.y = inner
float sdTorus(vec3 p, vec2 t)
{
	vec2 q = vec2(length(p.xz) - t.x, p.y);
	return length(q) - t.y;
}

float sdTorus82(vec3 p, vec2 t)
{
	vec2 q = vec2(length2Nth(p.xz, 2.0) - t.x, p.y);
	return length2Nth(q, 8.0) - t.y;
}

float sdTorus88(vec3 p, vec2 t)
{
	vec2 q = vec2(length2Nth(p.xz, 8.0) - t.x, p.y);
	return length2Nth(q, 8.0) - t.y;
}

// MARK: Scene
Surface scene(vec3 p)
{	
	vec3 floorColor = vec3(0);
	
	// Checker floor
	if (u_floorColor == 0)
	{
		floorColor = vec3(2.0 + 2.0 * mod(floor(p.x) + floor(p.z), 2.0));
	}
	else
	{
		floorColor = 5.0 * vec3(0.3, 0.5, 0.7);
	}
	
	// Always create the floor for the scene
	Surface floor = Surface(1, sdPlane(p, vec3(0.0, 1.0, 0.0), 2.0), floorColor);
	
	if (u_shaderScene == 0)
	{
		Surface sphere1 = Surface(2, sdSphere(p - vec3(-2, 0, 0), 1.0), vec3(0.4, 0.4, 1.0));
		Surface sphere2 = Surface(3, sdSphere(p - vec3(2, 0, 0), 1.0), vec3(0.0, 1.0, 0.0));
		
		Surface shape = opUnion(sphere1, sphere2);
		return opUnion(shape, floor);
	}
	// RoundedBox changes from sphere to box and back
	else if (u_shaderScene == 1)
	{
		float roundedBox = (sin(iTime) + 1.0) / 2.0;
		Surface shape = Surface(2, sdRoundBox(p, vec3(roundedBox), -(roundedBox - 1.0)), vec3(0.2, 0.4, 0.6));
		
		return opUnion(shape, floor);
	}
	// Repeated Boxes On The Ground
	else if (u_shaderScene == 2)
	{
		p.xz = mod(p.xz, 1.0) - vec2(0.5);
		Surface shape = Surface(2, sdBox(p - vec3(0.0,-1.25, 0.0), vec3(0.25)), vec3(0.2, 0.4, 0.6));
		
		return opUnion(shape, floor);
	}
	// Repeated Spheres In All Directions
	else if (u_shaderScene == 3)
	{
		p = mod(p.xyz, 1.0) - vec3(0.5);
		Surface shape = Surface(2, sdSphere(p, 0.25), vec3(0.2, 0.4, 0.6));
		
		return opUnion(shape, floor);
	}
	// A Sphere with boxes subtracted from it
	else if (u_shaderScene == 4)
	{
		Surface sphere = Surface(2, sdSphere(p, 2.0), vec3(0.2, 0.4, 0.6));
		
		p = mod(p.xyz, 0.5) - vec3(0.25);
		Surface box = Surface(3, sdBox(p, vec3(0.2)), vec3(0));
		
		Surface shape = opSubtract(sphere, box);
		return opUnion(shape, floor);
	}
	// One Box with a twist ;-)
	else if (u_shaderScene == 5)
	{
		// Twist Box
		p = rotateY(p, 0.5 * p.y);
		Surface shape = Surface(2, sdBox(p, vec3(1.0, 2.0, 1.0)), vec3(0.2, 0.4, 0.6));
		
		return opUnion(shape, floor);
	}
	// Twisted box with a sphere subtracted from it
	else if (u_shaderScene == 6)
	{
		Surface sphere = Surface(2, sdSphere(p, 1.3), vec3(0));
		// Twist Box
		p = rotateY(p, 0.5 * p.y);
		Surface box = Surface(3, sdBox(p, vec3(1.0, 2.0, 1.0)), vec3(0.2, 0.4, 0.6));
		
		Surface shape = opSubtract(box, sphere);
		return opUnion(shape, floor);
	}
	// Torus
	else if (u_shaderScene == 7)
	{
		p = rotateX(p, radians(90.0));
		Surface shape = Surface(2, sdTorus(p, vec2(1.0, 0.5)), vec3(0.2, 0.4, 0.6));
		
		return opUnion(shape, floor);
	}
	// Flat Torus
	else if (u_shaderScene == 8)
	{
		p = rotateX(p, radians(90.0));
		Surface shape = Surface(2, sdTorus82(p, vec2(1.0, 0.5)), vec3(0.2, 0.4, 0.6));
		
		return opUnion(shape, floor);
	}
	// Square Flat Torus
	else if (u_shaderScene == 9)
	{
		p = rotateX(p, radians(90.0));
		Surface shape = Surface(2, sdTorus88(p, vec2(1.0, 0.5)), vec3(0.2, 0.4, 0.6));
		
		return opUnion(shape, floor);
	}
}

Surface rayMarch(vec3 ro, vec3 rd)
{
	float depth = MIN_DISTANCE;
	Surface shape;
	
	for (int i = 0; i < MAX_MARCHING_STEPS; i++)
	{
		vec3 p = ro + depth * rd;
		shape = scene(p);
		depth += shape.distance;
		
		if (shape.distance < PRECISION || depth > MAX_DISTANCE)
		{
			break;
		}
	}
	
	shape.distance = depth;
	
	return shape;
}

vec3 createNormal(vec3 p)
{
	const float h = EPSILON;
	const vec2 k = vec2(1,-1);
	
	return normalize(
		k.xyy * scene(p + k.xyy * h).distance +
		k.yyx * scene(p + k.yyx * h).distance +
		k.yxy * scene(p + k.yxy * h).distance +
		k.xxx * scene(p + k.xxx * h).distance);
}

// k is higher value = sharper and lower value = softer
float createShadow(vec3 ro, vec3 rd, float mint, float maxt, float k)
{	
	float result = 1.0;
	float t = mint;
	
	for (int i = 0; i < 64; i++)
	{
		float distance = scene(ro + rd * t).distance;
		result = min(result, k * distance / t);
		t += distance;
		
		if (distance < EPSILON || t >= maxt)
		{
			break;
		}
	}
	
	return result;
}

// Phong lighting
vec3 createLight(vec3 lightDir, vec3 normal, vec3 rd, Material mat)
{
    // diffuse
    float diff = max(dot(lightDir, normal), 0.0);
    vec3 diffuse = diff * mat.diffuseColor;
    
    // specular
    if (mat.shininess > 0.0)
    {
		vec3 reflectDir = reflect(lightDir, normal);  
		float spec = pow(max(dot(rd, reflectDir), 0.0), mat.shininess);
		vec3 specular = mat.specularIntensity * spec * mat.specularColor;  
	
		return mat.ambientColor + diffuse + specular;
	}
	
    return mat.ambientColor + diffuse;
}

void mainImage(out vec4 fragColor, in vec2 fragCoord)
{
	vec2 uv = (fragCoord - 0.5 * iResolution.xy) / iResolution.y;
	vec3 backgroundColor = vec3(0.2, 0.4, 0.6);
	
	vec3 color = vec3(1.0);
	float theta = iTime * 0.5;
	
	// The ro and rd z axises are reversed so that the z axis is like opengl
	vec3 ro = vec3(0, 0,-8); // ray origin
	ro = rotateY(ro, theta); // Rotate the linear motion
	
	vec3 rd = normalize(vec3(uv, 1)); // ray direction
	rd = rotateY(rd, theta); // Rotate the angular motion
	
	Surface shape = rayMarch(ro, rd);
	
	if (shape.distance > MAX_DISTANCE)
	{
		color = backgroundColor;
	}
	else
	{
		vec3 p = ro + rd * shape.distance;
		vec3 normal = createNormal(p);
		
		// blue light
		vec3 lightPosition1 = vec3(-4, 5,-6);
		vec3 lightDirection1 = normalize(lightPosition1 - p);

		// red light
		vec3 lightPosition2 = vec3(4, 4, 0);
		vec3 lightDirection2 = normalize(lightPosition2 - p);
		
		color = shape.color;
		
		color *= 0.5 * clamp(createShadow(p, lightDirection1, 1.0, 10.0, 128.0), 0.5, 1.0);
		color *= 0.5 * clamp(createShadow(p, lightDirection2, 1.0, 10.0, 128.0), 0.5, 1.0);

		// id = 1 is the floor and the 0.5 and 0.4 are intensity
		if (shape.id > 1)
		{
			color += 0.3 * createLight(lightDirection1, normal, rd, blueLight(shape.color));
			color += 0.3 * createLight(lightDirection2, normal, rd, redLight(shape.color));
		}
		
		float fog = clamp((MAX_FOG - shape.distance) / (MAX_FOG - MIN_FOG), 0.0, 1.0);
		color = mix(backgroundColor, color, fog);
	}
				
	fragColor = vec4(color, 1.0);
}

// ************************** Do Not Remove **************************
void main(void)
{
	mainImage(mainFragColor, gl_FragCoord.xy);
}
// ************************** Do Not Remove **************************


</script>
</head>

<body onload="init();">
	<script src="webgl.js"></script>
	<script src="main.js"></script>
</body>
</html>
